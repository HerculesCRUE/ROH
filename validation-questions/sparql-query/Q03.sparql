PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix bibo: <http://purl.org/roh_edma/mirror/bibo#> 
prefix foaf: <http://purl.org/roh_edma/mirror/foaf#>
prefix roh: <http://purl.org/roh_edma#>
prefix vivo:  <http://purl.org/roh_edma/mirror/vivo#>
prefix uneskos: <http://purl.org/roh/unesco-individuals#>
prefix ro: <http://purl.org/obo/owl/ro#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT   ?researcher ?cites ?h_index ?i10_index (COUNT( distinct ?publication) AS ?n_publication)
         (COUNT( distinct ?publication_correspondingAuthor) AS ?n_correspondingAuthor) 
WHERE {
  ?researcher	roh:hasKnowledgeArea uneskos:120304;
  			    roh:hasPosition  ?position;
            roh:hasMetric ?metric.
  ?position a roh:ResearcherPosition;
            vivo:relates ?center.
  ?center a ?centerClass.
  
  OPTIONAL{ ?publication a bibo:Document;
                          bibo:authorList+| roh:seqOfAuthors ?authorList.
            ?authorList a rdf:Seq;
                          rdfs:member ?researcher.
           }
  OPTIONAL{ ?publication_correspondingAuthor a bibo:Document;
                                     roh:correspondingAuthor ?researcher.
          }
  OPTIONAL{?metric a roh:AuthorMetric;
                  roh:h-index ?h_index;
                  roh:i10-index ?i10_index;
                  roh:citationCount ?cites.}

  Filter(?centerClass = roh:ResearchGroup || ?centerClass=roh:ResearchInstitute)
}
GROUP BY ?researcher ?cites ?h_index ?i10_index 
ORDER BY DESC(?cites + ?h_index + ?i10_index + ?n_publication + ?n_correspondingAuthor)
LIMIT 10
