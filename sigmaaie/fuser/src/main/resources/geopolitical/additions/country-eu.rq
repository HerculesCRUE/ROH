PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX asio:         <http://purl.org/hercules/asio/core#>
PREFIX asioModules:  <http://purl.org/hercules/asio/modules#>
PREFIX atold:   <http://publications.europa.eu/resource/authority/>
PREFIX foaf:    <https://xmlns.com/foaf/0.1/>
PREFIX geonames: <http://purl.org/roh/mirror/geonames#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX vivo:    <http://purl.org/roh/mirror/vivo>
PREFIX obo:     <http://purl.org/roh/mirror/obo/ro#>
PREFIX roh:     <http://purl.org/roh#>

# For countries in europa.eu namespace, create a new
# URI in the roh namespace for the geonames:Feature version.
CONSTRUCT {
    ?xFeature a geonames:Feature .
    ?xFeature geonames:featureCode geonames:A.PCLI .
    ?xFeature rdfs:label ?label .
    ?xFeature skos:prefLabel ?prefLabel .
    ?xFeature skos:altLabel ?altLabel .
    ?xFeature asio:hasCode ?code .
    ?x skos:exactMatch ?xFeature .
    ?xFeature skos:exactMatch ?x .
} WHERE {
    ?x a skos:Concept .
    FILTER(REGEX(STR(?x), "http://publications.europa.eu/resource/authority/country/"))
    BIND(IRI(CONCAT(STR(roh:), "euCountry-", STRAFTER(STR(?x), "authority/country/"))) AS ?xFeature)
    ?x asio:hasCode ?code .
    OPTIONAL { ?x rdfs:label ?label }
    OPTIONAL { ?x skos:prefLabel ?prefLabel }
    OPTIONAL { ?x skos:altLabel ?altLabel }
    {
      ?x skos:inScheme asioModules:CountryClassification
    } UNION {
      ?x skos:inScheme atold:country
    } UNION {
      ?x skos:exactMatch ?y .
      ?y skos:inScheme asioModules:CountryClassification 
    } UNION {
      ?x skos:closeMatch ?y .
      ?y skos:inScheme asioModules:CountryClassification
    } UNION {
      ?x skos:exactMatch ?y .
      ?y skos:inScheme atold:country .
    } UNION {
      ?x skos:closeMatch ?y .
      ?y skos:inScheme atold:country .
    }
}

